# ---- History ----
HISTFILE=~/.zsh_history # Location of the history file
HISTSIZE=10000 # Maximum number of entries in the history file
SAVEHIST=10000 # Maximum number of lines in memory before writing to the file
# History options
setopt APPEND_HISTORY    # Append history instead of overwriting
setopt SHARE_HISTORY     # Share history across multiple terminals
setopt HIST_IGNORE_DUPS  # Ignore duplicate commands
setopt HIST_IGNORE_SPACE # Ignore commands starting with a space
setopt HIST_VERIFY       # Let you edit history before executing

# ---- Keybindings ----
# Delete key fix
bindkey '\e[3~' delete-char
# Insert key
bindkey '\e[2~' overwrite-mode
# Home and End keys
bindkey '\e[1~' beginning-of-line
bindkey '\e[4~' end-of-line
bindkey '\e[H' beginning-of-line
bindkey '\e[F' end-of-line
# Page Up and Page Down keys
bindkey '\e[5~' beginning-of-history
bindkey '\e[6~' end-of-history
# ctrl + arrow keys for word-by-word navigation
bindkey '\e[1;5C' forward-word
bindkey '\e[1;5D' backward-word
# Shift + arrow keys for character selection
_shift_select_forward () {
    if ((REGION_ACTIVE)); then
        zle .forward-char
    else
        zle .set-mark-command && zle .forward-char
    fi
}
zle -N _shift_select_forward
_shift_select_backward () {
    if ((REGION_ACTIVE)); then
        zle .backward-char
    else
        zle .set-mark-command && zle .backward-char
    fi
}
zle -N _shift_select_backward
bindkey '\e[1;2C' _shift_select_forward
bindkey '\e[1;2D' _shift_select_backward
# ctrl + Shift + arrow keys for word selection
_shift_select_forward_word () {
    if ((REGION_ACTIVE)); then
        zle .forward-word
    else
        zle .set-mark-command && zle .forward-word
    fi
}
zle -N _shift_select_forward_word
_shift_select_backward_word () {
    if ((REGION_ACTIVE)); then
        zle .backward-word
    else
        zle .set-mark-command && zle .backward-word
    fi
}
zle -N _shift_select_backward_word
bindkey '\e[1;6C' _shift_select_forward_word
bindkey '\e[1;6D' _shift_select_backward_word

# Shift + Home/End for line selection
_shift_select_to_start () {
    if ((REGION_ACTIVE)); then
        zle .beginning-of-line
    else
        zle .set-mark-command && zle .beginning-of-line
    fi
}
zle -N _shift_select_to_start
_shift_select_to_end () {
    if ((REGION_ACTIVE)); then
        zle .end-of-line
    else
        zle .set-mark-command && zle .end-of-line
    fi
}
zle -N _shift_select_to_end
bindkey '\e[1;2H' _shift_select_to_start
bindkey '\e[1;2F' _shift_select_to_end

# Ctrl + Home/End for start/end of buffer
bindkey '\e[1;5H' beginning-of-buffer
bindkey '\e[1;5F' end-of-buffer

# Ctrl + Shift + Home/End for buffer selection
_shift_select_to_start_buffer () {
    if ((REGION_ACTIVE)); then
        zle .beginning-of-buffer
    else
        zle .set-mark-command && zle .beginning-of-buffer
    fi
}
zle -N _shift_select_to_start_buffer
_shift_select_to_end_buffer () {
    if ((REGION_ACTIVE)); then
        zle .end-of-buffer
    else
        zle .set-mark-command && zle .end-of-buffer
    fi
}
zle -N _shift_select_to_end_buffer
bindkey '\e[1;6H' _shift_select_to_start_buffer
bindkey '\e[1;6F' _shift_select_to_end_buffer

# Ctrl+C to copy selection
_copy_region() {
    if ((REGION_ACTIVE)); then
        zle copy-region-as-kill
        echo -n "\033]52;c;$(base64 <<< $CUTBUFFER)\a"
    fi
}
zle -N _copy_region
bindkey '^C' _copy_region

# Delete key to delete selection
_delete_region() {
    if ((REGION_ACTIVE)); then
        zle kill-region
    else
        zle delete-char
    fi
}
zle -N _delete_region
bindkey '\e[3~' _delete_region

# Backspace to delete selection
_backward_delete_region() {
    if ((REGION_ACTIVE)); then
        zle kill-region
    else
        zle backward-delete-char
    fi
}
zle -N _backward_delete_region
bindkey '^?' _backward_delete_region

# ---- Paths ----
PATH=$PATH:/home/$USER/go/bin
PATH=$PATH:/home/$USER/.local/bin
# pnpm
export PNPM_HOME="$HOME/.local/share/pnpm"
case ":$PATH:" in
*":$PNPM_HOME:"*) ;;
*) export PATH="$PNPM_HOME:$PATH" ;;
esac
# bun
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"
# opencode
export PATH=/home/jfalava/.opencode/bin:$PATH

# ---- Aliases ----
alias update-all="sudo apt update && sudo apt upgrade && sudo apt autoremove && nix profile upgrade '.*' --exclude '.*nix.*' 2>/dev/null || echo 'No packages to upgrade' && nix-collect-garbage -d"
alias cls='clear'
alias ff='fastfetch'
alias tf='terraform'
alias l='eza --color=always --long --git --no-filesize --icons=always'
alias ls='eza --color=always --long --git --no-filesize --icons=always --all --color-scale-mode=gradient'
nix-install() {
  nix profile add "nixpkgs#$1"
}
nix-uninstall() {
  nix profile remove "nixpkgs#$1"
}
nix-search() {
  nix search nixpkgs "$1"
}

# ---- Sources ----
# zsh plugins
if [ -f $HOME/.nix-profile/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]; then
  source $HOME/.nix-profile/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi
if [ -f $HOME/.nix-profile/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]; then
  source $HOME/.nix-profile/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi
# nix
source ~/.nix-profile/etc/profile.d/nix.sh
# zoxide
eval "$(zoxide init zsh)"
# SSH
if [ -z "$SSH_AUTH_SOCK" ] || [ ! -S "$SSH_AUTH_SOCK" ]; then
    eval "$(ssh-agent -s)"
fi
# starship
eval "$(starship init zsh)"
# initialize zsh completions
autoload -Uz compinit
compinit
# keyring daemons
if ! pgrep -f "dbus-daemon" > /dev/null; then
  dbus-daemon --session --fork 2>/dev/null
fi
if ! pgrep -f "gnome-keyring-daemon" > /dev/null; then
  gnome-keyring-daemon --start --components=secrets 2>/dev/null
fi
